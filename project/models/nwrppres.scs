* Path, Component, Release: cmos7base/rel/Spectre/models/nwrppres.scs, tech7_models, mod_cmos7base
* CMVC Revision: 1.2 09/09/08 09:24:58
*
*>  IBM 7RF/7WL  nwrppres    RP Poly Resistor (Over NWell)
*
***********************************************************************
*
*  Distributed R-C network which accounts for end resistance effects,
*  parasitic capacitance and includes a fixed substrate resistance.
*
*          Rend        Rmid                     Rmid        Rend
*  in  o--/\/\/\--o---/\/\/\---------o---------/\/\/\---o--/\/\/\--o  out
*                 |                  |                  |
*                 |                  |                  |
*               ----               -----              -----
*          Cpcx ----          Cpcx -----         Cpcx -----
*                 |                  |                  |
*                 |                  |                  |
*                 \                  \                  \
*             Rg  /               Rg /               Rg /
*                 \                  \                  \
*                 |                  |                  |
*                 o------------------o------------------o
*                                    |
*   Parasitic capacitance is         |
*   divided among C elements       ----
*   in 1/6 - 2/3 - 1/6 ratios       /\   Dnwsx
*                                  /  \          Diode includes fixed
*                                  ----          substrate resistance
*                                    |
*                                    |
*                                    o substrate (sx)
*
***********************************************************************
*
*  DIMENSIONS: W = Design width  of PC
*              L = Design length of OP
*
*  SYNTAX:     Specify both width and length:
*              xxx  in out nw sx  nwrppres (w=2u  l=5u)
*
*       OR     Specify width and resistance:
*              xxx  in out nw sx  nwrppres (w=2u  r=520)
*
*  SERIES:        s  --> Defined model parameter,   Reff = Rbar * s
*
*  PARALLEL:      m  --> Implicit model parameter,  Reff = Rbar / m
*
*              pbar  --> Defined model parameter,   Reff = Rbar
*
*               par  --> Explicit device multiplicity (par must equal m in netlist)
*
*  OPTIONAL:   dtemp --> Device temperature difference with respect to
*                        the simulated circuit temperature (deg C).
*
*                rsx --> Fixed substrate resistance, default = 50 ohm.
*
*  NOTES:
*
*  1. Model supports only rectangular resistors with contacts at each
*     end of the resistor bar. "Dogbone" or multiple tap layouts are 
*     not supported.
*
*  2. Model assumes minimum groundrule spacings used for placing contacts
*     to calculate the end resistance effects based on OP design length.
*
*  3. Model assumes area and perimeter for the NW-SX parasitic capacitance
*     is equal to that calculated for the PC over STI capacitance.
*
*
* IBM CONFIDENTIAL
* (C) 2009 IBM Corporation
*
***********************************************************************
simulator lang=spectre

subckt nwrppres  (in out nw sx)
parameters       w=2u l=-1u r=-1  s=1 pbar=1  dtemp=0 rsx=50  par=1
*
* Check for minimum width, calculate effective width
+ opwdth = max(w,0.32u) + oprpdwf
*
* Calculate op length, assumes w,r and groundrule spacings for contacts
+ rendn = 1u*(exp(oprprenn) + 1)
+ pleqn = ((r*pbar*(w+oprpdwn) - 2*rendn) / oprprsn) - oprpdln
*
* Select op length, user input takes precedence over calculated length
+ plnom = max(l,0) + ((l-abs(l))/(2*l))*pleqn
*
* Check for minimum length, calculate effective length
+ oplen = max(plnom,1.1u) + oprpdlf
*
* Resistance calculation
+ rmid = oprprsf * (oplen / opwdth) / 2
+ rend = 1u*(exp(oprprenf) + 1) / opwdth
*
* Calculate area, perimeter for capacitance
* Subtract delta w, delta l to obtain drawn dimensions,
* Then include region outside of op mask
+ opcaplen  = 2 * ((oplen-oprpdln) + 2*oppcendf + (opwdth-oprpdwn))
+ opcaparea = ((oplen-oprpdln) + 2*oppcendf) * (opwdth-oprpdwn)
*
* Partition capacitance area, perimeter
+ apcx = opcaparea / 3
+ ppcx = opcaplen / 3
*
* Mis-match Calculation
+ opma   = oprpma * oprpma / (oplen * opwdth * 1e12)
+ opml   = oprpml * oprpml / (opwdth * oplen * oplen * 1e18)
+ opmw   = oprpmw * oprpmw / (opwdth * opwdth * 1e12)
+ op3sig = 0.01 * sqrt(opma + opml + opmw)
+ opmm   = op3sig * 0.7071 / sqrt(s*pbar*par)
+ mmatr  = 1 + (opmm * mmres)
*
* Effective resistance from conductance
+ rg = 1/(12e2*opwdth + 4.3e2*oplen)
*
*Flicker Noise
+ lnoi = oplen - oprpdlf
+ wnoi = opwdth - oprpdwf
+ rtot = 2*rmid+2*rend
+ k1 = 13.44e-23*(wnoi/lnoi)*(rtot/rmid)*(rtot/rmid)/(wnoi*wnoi)/(s*pbar)

* Resistor network
rea  in   1  resistor  r=rend*mmatr*s/pbar  trise=dtemp  tc1=oprprntc
reb   5 out  resistor  r=rend*mmatr*s/pbar  trise=dtemp  tc1=oprprntc
rma   1   3  noise_res r=rmid*mmatr*s/pbar  trise=dtemp  tc1=oprprstc
rmb   3   5  resistor  r=rmid*mmatr*s/pbar  trise=dtemp  tc1=oprprstc

cpca  1  11  capacitor c=(  (apcx*oppcdown + ppcx*oppcfrng)/2)*s*pbar
cpcb  3  31  capacitor c=(2*(apcx*oppcdown + ppcx*oppcfrng))  *s*pbar
cpcc  5  51  capacitor c=(  (apcx*oppcdown + ppcx*oppcfrng)/2)*s*pbar

rga   11 nw  resistor r=6*rg/(s*pbar)
rgb   31 nw  resistor r=1.5*rg/(s*pbar)
rgc   51 nw  resistor r=6*rg/(s*pbar)

dnwsx sx nw  dcsx  area=opcaparea*s*pbar pj=opcaplen*s*pbar trise=dtemp

model noise_res resistor
+ l = 1
+ kf = k1
+ af = 2 
+ wdexp = 0
+ ldexp = 0
+ leexp = 0
+ weexp = 0

* NWell to SX Diode
* Note: Diode Rseff = rs / area --> Adjust rs such that Rseff = rsx.
*
model dcsx diode
+ level = 1
+  tlev = 2
+ tlevc = 1
+  tnom = 25
+    eg = 1.12
+   xti = 3.0
+  gap1 = 473u
+  gap2 = 636
+    is = js_nw
+   isw = jsw_nw
+     n = n_nw
+    ns = n_nw
+    rs = rsx*opcaparea*s*pbar
+   cjo = cj_nw
+   cta = cta_nw
+    vj = pb_nw
+   pta = pta_nw
+     m = mj_nw
+  cjsw = cjsw_nw
+   ctp = ctp_nw
+  vjsw = pbsw_nw
+   ptp = ptp_nw
+  mjsw = mjsw_nw
+    bv = 10.0
+  imax = 1e15

ends nwrppres
