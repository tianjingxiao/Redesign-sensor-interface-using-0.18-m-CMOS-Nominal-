* Path, Component, Release: cmrf7sf/relAM/Spectre/models/ind.scs, rf7sf_models, mod_cmrf7sf
* CMVC Revision: 1.6 11/08/30 12:31:35
***************************************************************************
*
* Single Level Octagonal Spiral Inductor
*
***************************************************************************
*
* The inductor model calls indstack subcurcuit.
*                                  +-------+
*                                  |       |
*                                  o       o
*                  +------------------------------------+
*  out  o---------o|outpr        inpr     inse     outse|o----------o  in
*                  |                                    |
*                  |         "indstack subcircuit"      |
*                  |                                    |
*                  |                 ref                |
*                  +------------------------------------+
*                                      o
*                                      |
*                                      o
*                      ref (substrate/groundplane)
*******************************************************************************
*
* NOTES:
*
*  1. Outer dimension (x) may vary from 100um to 450um.
*
*  2. Supported spiral widths (w) may vary from 5um to 25um.
*
*  3. Turn to turn space (s) may vary from 2.8um to 5um depending on spiral width.
*
*  4. Number of turns (n) may vary from 1 to filled-in by 1/4-turn increments.
*
* IBM CONFIDENTIAL
* (C) 2011 IBM Corporation
*
*******************************************************************************
simulator lang=spectre
*
subckt ind (out in ref)
parameters
+    x      =  100u      // Largest outer dimension of octagonal spiral (m)
+    w      =  5u        // Width of spiral turn (m)
+    s      =  3u        // Turn to turn space (m)
+    n      =  2         // Number of turns in 1/4-turn increments.
+    wu     =  15u       // Width of underpass (m)
+    nlev   =  5         // Number of metal levels in the design.
+    grnd   =  -1        // Indicator for grounding scheme under the spiral.
*                        // -1 = BB (Default)
*                        // -2 = M1 groundplane
*                        // -5 = HR groundplane
+    dtemp  =  0         // Temperature difference between element and
*                        // circuit temperature (deg C).
*
+ cmult= 1
+ lmult= 1
+ rmult= 1
+ lenmult= 1
+ px= x*lenmult
+ pw= w*lenmult
+ ps= s*lenmult
+ ppwu= wu*lenmult
+ rxflfrac=0.20
+ rxeff=oppcstif-2*rxflfrac*oppcstif
+ pcflfrac=0.20
+ pceff=oppcthk-2*pcflfrac*oppcthk
+ m1flfrac=0.09
+ m1teff=m1t-2*m1flfrac*m1t
+ m2flfrac=0.00
+ m2teff=m2t-2*m2flfrac*m2t
+ m3flfrac=0.00
+ m3teff=m3t-2*m3flfrac*m3t
+ m4flfrac=0.00
+ m4teff=m4t-2*m4flfrac*m4t
+ m5flfrac=0.00
+ m5teff=m5t-2*m5flfrac*m5t
+ mtflfrac=0.00
+ mtteff=mtt-2*mtflfrac*mtt
+ amflfrac=0.00
+ amteff=amt-2*amflfrac*amt
*
* Conversion of grnd parameter to "bptype" for indstack subcircuit
+ bptype = -1*((grnd==-1)||(grnd==-5)) + -2*(grnd==-2)
*
* Calculations of primary and secondary.
+ pprt=amt
+ pdelpr=delam
+ pprrs=amrs
+ temcpr=amrs*tamrs
+ temcprnum=amrs
+ psect=mtt
+ pdelsec=delmt
+ psecrs=mtrs
+ temcse=mtrs*tmtrs
+ temcsenum=mtrs
+ psecprt=mtamt
*
* Calculations of stack height
+ h7= rxeff+pceff+capct+m1teff+m1m2t+m2teff+m2m3t+m3teff+m3m4t+m4teff+m4m5t+m5teff+mxmtt+psect+psecprt+pprt
+ h6= rxeff+pceff+capct+m1teff+m1m2t+m2teff+m2m3t+m3teff+m3m4t+m4teff+mxmtt+psect+psecprt+pprt
+ h5= rxeff+pceff+capct+m1teff+m1m2t+m2teff+m2m3t+m3teff+mxmtt+psect+psecprt+pprt
+ h4= rxeff+pceff+capct+m1teff+m1m2t+m2teff+mxmtt+psect+psecprt+pprt
+ h3= rxeff+pceff+capct+m1teff+mxmtt+psect+psecprt+pprt
+ phpr= (nlev==7)*h7+(nlev==6)*h6+(nlev==5)*h5+(nlev==4)*h4+(nlev==3)*h3-pprt
+ phsec= phpr-psect-psecprt
*
* Calculations of M1 shield stack height.
+ phm1sx= oppcstif+oppcthk+capct+m1t
+ phpreff= phpr-(grnd==-2)*phm1sx
+ phseceff= phsec-(grnd==-2)*phm1sx
* Calculations of Temc. Coefficient
+ ptprrs= temcpr/temcprnum
+ ptsers= temcse/temcsenum
*
* PR Single Layer Coefficients
+ ppcprs=e0*4.100/phpreff
+ ppcus=e0*4.100/phseceff
+ ppcpru=e0*4.100/psecprt
+ ppcpruf=e0*4.100*(2*1.064/(pi-2)*log(pi/2)+2*1.064/pi*log(1+pprt/psecprt))
+ ppcprsf=e0*4.100*(2*0.835/(pi-2)*log(pi/2)+2*1.537/pi*log(1+pprt/phpreff))
+ ppcusf=e0*4.100*(2*0.592/(pi-2)*log(pi/2)+2*2.803/pi*log(1+psect/phseceff))
+ palphapru=atan(ps/1.257/psecprt)
+ ppcmpruf=e0*4.100*(0.695*(1-cos(2*palphapru))/(2*palphapru-sin(2*palphapru))*log(2*palphapru/sin(2*palphapru)))
+ palphapr=atan(ps/0.687/phpreff)
+ ppcmprsf=e0*4.100*(0.445*(1-cos(2*palphapr))/(2*palphapr-sin(2*palphapr))*log(2*palphapr/sin(2*palphapr)))
+ pbeta=pi/2-2*palphapr
+ ppcprpr=e0*4.100*(1/pi*log(1+pw/ps)+0.000/(pi-2)*log(pi/2)+1.177*pprt/ps+1.192/2*(1-cos(pbeta))/(pbeta-sin(pbeta))*log(pbeta/sin(pbeta)))
*
xstack out 2 in 2 ref indstack
+ xp= px
+ xs= px
+ wp= pw
+ ws= pw
+ sp= ps
+ ss= ps
+ np= n
+ ns= n
+ pwu= ppwu
+ pstack= 0
+ pgrnd= bptype
+ gprs= m1rs
+ tgprs= tm1rs
+ ctrs= m1rs
+ tctrs= tm1rs
+ rsx= ((grnd==-1)||(grnd==-2))*rsub + (grnd==-5)*rsubhr
+ trsx= trsub
+ pdtemp= dtemp
+ delpr= pdelpr
+ prrs= pprrs
+ prt= pprt
+ tprrs= ptprrs
+ delse= pdelpr
+ sers= pprrs
+ sect= pprt
+ tsers= ptprrs
+ delup= pdelsec
+ uprs= psecrs
+ tuprs= ptsers
+ upt= psect
+ cprs= ppcprs
+ cprsf= ppcprsf
+ cmprsf= ppcmprsf
+ cprpr= ppcprpr
+ cpru= ppcpru
+ cpruf= ppcpruf
+ cmpruf= ppcmpruf
+ cses= ppcprs
+ csesf= ppcprsf
+ cmsesf= ppcmprsf
+ csese= ppcprpr
+ cprse= 1f
+ cprsef= 1f
+ cmprsef= 1f
+ cus= ppcus
+ cusf= ppcusf
+ sxthk= pmsxt
*
* optimization coefficients
+ prscrfmin = 1.2e-3
+ prscrfmax = 6.0e-3
+ prscrfa = 6.0e-3
+ prscrfb = -0.4e-3
+ prfb = 9e-6
+ ppowa = 0
+ ppowb = 0
+ pamax = 4600
+ paa = -13000
+ pab = 100
+ condsib = 1
+ prgptb = 100
+ rsxcorrb = 1.3
+ csxcormax = 1.5
+ csxcorra = 1.2e-3
+ csxcorrb = -0.2891
+ pcmprsb = 1
+ pcmsesb = 1
+ pcofprsb = 1 - (n>1.5)*0.2
+ pcofsesb = 1.5 - (n>1.5)*0.5
+ pcoprseb = 1
+ pcmprseb = 1
+ posplps = (bptype==-1)*0.499 + (bptype==-2)*0.3
+ plscrfmax = 0.4
+ plscrfa = 0.15
+ plscrfb = 0.05
+ pcoo = 0.5 + (grnd==-5)*0.15
*
ends ind
