* Path, Component, Release: cmos7base/rel/Spectre/models/nfet33_psp.scs, tech7_models, mod_cmos7base 
* CMVC Revision: 1.4 12/09/26 07:48:58 
*
*  IBM 7RF/7WL  nfet33psp     2.5V and 3.3V NFET subcircuit
*
***************************************************************************
*
*  SYNTAX:  xxx ( d g s b ) nfet33psp l=nnn w=nnn
*
*  REQUIRED:
*            +        w = nnn  <- Design width of RX (meters)
*            +        l = nnn  <- Design length of PC (meters)
*  OPTIONAL:
*            +       as = 0    <- Source diffusion area (meters^2)
*            +       ad = 0    <- Drain diffusion area (meters^2)
*            +       ps = 0    <- Source diffusion perimeter in (meters)
*            +       pd = 0    <- Drain diffusion perimeter in (meters)
*            +      nrd = 0    <- Number of squares of drain diffusion
*            +      nrs = 0    <- Number of squares of source diffusion
*            +    dtemp = 0    <- Device temp rise from ambient (deg C)
*            +       nf = 1    <- number of gate stripes or "fingers" in device
*            +  m (par) = 1    <- number of parallel instances (multiplicity)
*            +     gcon = 1    <- Gate connection switch (1->single-ended,
*                                                         2->double-ended)
*            +      rsx = 50   <- body resistance
*            +       rf = 0    <- RF device flag (0-> nfet33 pcell
*                                                 1-> nfet33_rf pcell)
*            +      gns = 0    <- ESD hierarchical cell layout option
*                                 (0-> silicided gate, 1-> non-silicided gate)
*
*  NOTES:
*
*  1. Source/Drain parasitic calculations contained in this file will
*     override any values passed in the instance call for the following
*     parameters: nrs, nrd, as, ad, ps, pd.
*
*  2. Calculations assume input design width represents width of each
*     finger.
*
*  3. Calculations based on PCELL scaling rules and assume that for a device
*     with an even number of fingers, the source is the outer diffusion.
*
* SUBCIRCUIT SCHEMATIC:
*
*                cwgd
*                 | |             drain
*        o--------| |---------------o----------------------------------o
*        |        | |               |                                  |
*        |                          |     Ddb (Includes internal       |
*        |                          /     FET drain area and STI       |
*        |                   rdext  \     bounded perimeter)           |
*        |                          /                                  |
*        |                          |______|/|__________             ----- cwds
*        |                      d1  |      |\|          |            -----
*        |                          |                   |              |
*        |                          |                   |              |
*        |                    |_____|                   |              |
*        |              g1  | |         b1              |     bulk     |
*   gate o-----\/\/\/---o---| |  |------o----\/\/\/-----o-------o      |
*        |      rgate       | |_____          rsb       |              |
*        |                    |     |                   |              |
*        |                          |                   |              |
*        |                          |                   |              |
*        |                      s1  |______|/|__________|              |
*        |                          |      |\|                         |
*        |                          \                                  |
*        |                   rsext  /     Dsb (Includes internal       |
*        |                          \     FET source area and STI      |
*        |                          |     bounded perimeter)           |
*        |        | |               |                                  |
*        o--------| |---------------o----------------------------------o
*                 | |             source
*                cwgs
*
* IBM CONFIDENTIAL
* (C) 2012 IBM Corporation
*
***************************************************************************
simulator lang=spectre
inline subckt nfet33psp (d g s b)
parameters
+  l=0.4u w=2u nf=1 par=1 gns=0
+  as=0 ad=0 ps=0 pd=0 nrd=0 nrs=0 
+  gcon=1 rsx=50 dtemp=0 rf=0
*
+  zad=ad zas=as znrd=nrd znrs=nrs zpd=pd zps=ps
*
* Gate bounded perimeter (identical for source and drain)
+  pgate=(w+ndelrx)
*
* STI S/D bounded perimeter (subtract gate bounded from total passed in)
+  pstis=max(0,(zps-pgate))
+  pstid=max(0,(zpd-pgate))
*
* Mis-match calculations: function of w, l, nf and multiplicity (par)
+  kvt=1.05e-8     kvtw=-0.11u   kvtl=0.2924u
+  kmb=6.0877e-10  kmbw=-0.10u   kmbl=-0.4665u
+  sigvth=(kvt/sqrt((w-kvtw)*nf*(l-kvtl)))
+  sigbeta=(kmb/sqrt((w-kmbw)*nf*(l-kmbl)))
+  mmvth0 = (sigvth * mvth0)          // vth0 mis-match
+  mmbeta = (sigbeta * mbeta)         // beta mis-match
*
* Flicker noise: statistics function of w, l, nf and multiplicity (par)
+  noi_scale=noistdn-min(log(sqrt(w*l*nf*par/noidevan)),0)
+  noi_mul=exp(dnoin33*noi_scale)
*
* Gate resistance calculation (function of w, l, & nf)
+  rge1=((pcrsi*(1-gns)+opnpcrsf*gns)/(nf*gcon))*((0.3u+w/(3*gcon))/(l+lwbpc))
+  rgw1=(1.762e-4/((w-2*wint_n33)*nf)+1.246)
+  rgl1=(2.414e6*(l-2*lint_n33)+0.9962)/2.35
*
* Substrate resistance calculation for rf=1 case (function of w, l and nf)
+  rsw1=(191.9*log10(1/((w-2*wint_n33)*nf))-599.7)
+  rsl1=(3.374e8*(l-2*lint_n33)-13.11)/129
*
* Short Channel Switch (2.5V devices)
+  sch=(l<0.4u)
*
* Node voltage warning checks
+  pvlim  = 3.6*(1-sch) + 2.75*sch
+  pblim  = 6.0*(1-sch) + 4.6*sch
*
if (gbv==2) {
 vds_check  assert dev=nfet33psp param=vds       min=0 max=pvlim message="Vds exceeds limit" level=warning
 vgs_check  assert dev=nfet33psp param=vgs       min=-pvlim max=pvlim message="Vgs exceeds limit" level=warning
 vgd_check  assert dev=nfet33psp expr=(vgs-vds)  min=-pvlim max=pvlim message="Vgd exceeds limit" level=warning
 vgb_check  assert dev=nfet33psp expr=(vgs+vsb)  min=-pblim max=pblim message="Vgb exceeds limit" level=warning
 vsb_check  assert dev=nfet33psp param=vsb       max=pblim message="Vsb exceeds limit" level=warning
 vsb_chkfwd assert dev=nfet33psp param=vsb       min=-fwdlim message="Vsb source-body diode is being forward-biased" level=warning
 vdb_check  assert dev=nfet33psp expr=(vsb+vds)  max=pblim message="Vdb exceeds limit" level=warning
 vdb_chkfwd assert dev=nfet33psp expr=(vsb+vds)  min=-fwdlim message="Vdb drain-body diode is being forward-biased" level=warning
}
*
nfet33psp d1 g1 s1 b1 nch33 l=l w=w as=1e-15 ad=1e-15 ps=pgate pd=pgate mult=nf
rdext  d1  d resistor r=(znrd*rsh_n33/nf)
rsext  s1  s resistor r=(znrs*rsh_n33/nf)
rgate  g1  g resistor r=(rge1+rgw1*rgl1)
rsb     b b1 resistor r=((1-rf)*rsx + rf*rsl1*rsw1)
cwgd    g d  capacitor c=1e-18+rf*(5.7776e-11*w*nf+(0.1744*l*(nf-1)+0.1337*l*nf+0.0749*(nf-1))*1e-11*w*nf)
cwgs    g s  capacitor c=1e-18+rf*(5.7776e-11*w*nf+(0.1744*l*(nf-1)+0.1337*l*nf+0.0749*(nf-1))*1e-11*w*nf)
cwds    d s  capacitor c=1e-18+rf*(1.1e-17*w*nf/(l+3.2e-7)+5.5e-18*w*nf/(l+4e-7)+1.6e-11*(l+6e-7)*(nf-1)+2.8e-11*w*(int(nf/2)+int((nf-1)/2)))
ddb     b d1 ndio33 area=zad pj=pstid m=nf trise=dtemp
dsb     b s1 ndio33 area=zas pj=pstis m=nf trise=dtemp
*
***************************************************************************
* NFET33 PSP Model
***************************************************************************
model nch33 psp1020
+ level=1023
+ type=n
+ tr=25
***************************************************************************
* Switch Settings
***************************************************************************
+ swgidl=0
+ swigate=0
+ swimpact=1
+ swjuncap=3
***************************************************************************
* Parameters With Process Variation
***************************************************************************
+ lap=lint_psp_n33
+ lov=lov_psp_n33
+ novo=2.49e+24
+ nsubo=3.287e+23
+ rsw1=rsw_psp_n33
+ thesatl=0.45
+ toxo=tox_psp_n33
+ toxovo=tox_psp_n33
+ uo=(uo_psp_n33+mmbeta)
+ vfbo=(vfb_psp_n33+mmvth0)
+ wot=wint_psp_n33
+ cjorbot=cj_psp_n33
+ cjorsti=cjsg_psp_n33
+ cjorgat=cjsg_psp_n33
***************************************************************************
* Core Parameters
***************************************************************************
+ a1l=-0.04735 
+ a1o=9.596  
+ a1w=0  
+ a2o=22.31  
+ a3l=-0.09488  
+ a3o=1.251  
+ a3w=0  
+ a4l=-0.28  
+ a4o=0.18  
+ a4w=0  
+ agidlw=0  
+ alp1l1=0.00632  
+ alp1l2=0.1526  
+ alp1lexp=0.7455  
+ alp1w=0.002128  
+ alp2l1=1e-10  
+ alp2l2=1.762e-09  
+ alp2lexp=1.471  
+ alp2w=0  
+ alpl=0.01215  
+ alplexp=0.8074  
+ alpw=0.225  
+ axl=0.639  
+ axo=19.2  
+ betw1=-0.001059  
+ betw2=-0.07596  
+ bgidlo=41  
+ cfbo=0.5  
+ cfl=0.00052  
+ cflexp=2.5  
+ cfrw=0  
+ cfw=0  
+ cgbovl=0  
+ cgidlo=0  
+ chibo=3.1  
+ csl=0  
+ cslexp=0  
+ cslw=0  
+ cso=0  
+ csw=0  
+ ctl=0.02511  
+ ctlexp=1.277  
+ ctlw=0  
+ cto=9.188e-17  
+ ctw=0  
+ dlq=-3e-08  
+ dnsubo=0  
+ dphibl=4.2e-08  
+ dphiblexp=7.816  
+ dphiblw=0  
+ dphibo=-0.01578  
+ dphibw=0  
+ dta=dtemp  
+ dwq=0  
+ fbet1=0  
+ fbet1w=0  
+ fbet2=0  
+ fetao=1.723  
+ fol1=0.2396 
+ fol2=-0.014 
+ gc2o=0.375  
+ gc3o=0.063  
+ gcoo=0  
+ iginvlw=0  
+ igovw=0  
+ kuo=0  
+ kvsat=0  
+ kvtho=0  
+ lkuo=0  
+ lkvtho=0  
+ llodkuo=0  
+ llodvth=0  
+ lodetao=1  
+ lp1=2.184e-08  
+ lp1w=0  
+ lp2=1.505e-08  
+ lpck=2.5e-07  
+ lpckw=0  
+ lvarl=0  
+ lvaro=0  
+ lvarw=0  
+ mueo=0.5213  
+ muew=-0.1127  
+ npck=1.6e+23  
+ npckw=0  
+ npl=0  
+ npo=7.758e+25  
+ nslpo=0  
+ nsubw=0  
+ pkuo=0  
+ pkvtho=0  
+ qmc=1  
+ rsbo=0  
+ rsgo=0.04798  
+ rsw2=-3.002e-12  
+ saref=1e-06  
+ sbref=1e-06  
+ sta2o=0  
+ stbetl=0  
+ stbetlw=0  
+ stbeto=1.624  
+ stbetw=-0.085  
+ stbgidlo=0  
+ stcso=0  
+ stetao=0  
+ stigo=2  
+ stmueo=0.3193  
+ strso=-0.39  
+ stthemuo=0.7976  
+ stthesatl=-0.02  
+ stthesatlw=-0.05943  
+ stthesato=1.5  
+ stthesatw=0.2  
+ stvfbl=0.05  
+ stvfblw=0.05  
+ stvfbo=0.00075  
+ stvfbw=0  
+ stxcoro=0  
+ themuo=2.549  
+ thesatbo=0  
+ thesatgo=0.1551  
+ thesatlexp=1.0  
+ thesatlw=0.0475  
+ thesato=0.03854  
+ thesatw=-0.1585  
+ tkuo=0  
+ vfbl=vfbl_psp_n33
+ vfblw=vfblw_psp_n33
+ vfbw=0.015  
+ vnsubo=0  
+ vpo=0.01439  
+ wbet=1.319e-07  
+ wkuo=0  
+ wkvtho=0  
+ wlod=0  
+ wlodkuo=0  
+ wlodvth=0  
+ wseg=2.682e-09  
+ wsegp=1e-10  
+ wvarl=0  
+ wvaro=0  
+ wvarw=0  
+ xcorl=2.183  
+ xcorlw=0  
+ xcoro=0.001  
+ xcorw=1.6e-06  
***************************************************************************
* Junction Diode Parameters
***************************************************************************
+ trj=25
+ imax=1000
***************************************************************************
*    Capacitance Parameters
***************************************************************************
+ vbirbot=pb_n33
+ vbirsti=pbsg_n33
+ vbirgat=pbsg_n33
+ pbot=mj_n33
+ psti=mjsg_n33
+ pgat=mjsg_n33
***************************************************************************
*    Ideal Current Parameters
***************************************************************************
+ phigbot=1.033405
+ phigsti=1.033405
+ phiggat=1.033405
+ idsatrbot=js_n33
+ idsatrsti=jsw_n33
+ idsatrgat=jsw_n33
+ xjunsti=1e-07
+ xjungat=1e-07
+ csrhbot=0
+ csrhsti=0
+ csrhgat=0
***************************************************************************
*    Trap-Assisted Tunneling Parameters
***************************************************************************
+ ctatbot=0
+ ctatsti=0
+ ctatgat=0
+ mefftatbot=0.25
+ mefftatsti=0.25
+ mefftatgat=0.25
***************************************************************************
*    Band-to-Band Tunneling Parameters
***************************************************************************
+ cbbtbot=0
+ cbbtsti=0
+ cbbtgat=0
+ fbbtrbot=1e+09
+ fbbtrsti=1e+09
+ fbbtrgat=1e+09
+ stfbbtbot=0
+ stfbbtsti=0
+ stfbbtgat=0
***************************************************************************
*    Avalanche and Breakdown Parameters
***************************************************************************
+ vbrbot=1001
+ vbrsti=1001
+ vbrgat=1001
+ pbrbot=4
+ pbrsti=4
+ pbrgat=4
***************************************************************************
* Noise Parameters
***************************************************************************
+ nfalw=(1.50e25*noi_mul)
+ nfblw=(2.90e9*noi_mul)
+ nfclw=(-9.00e-8*noi_mul)
+ efo=1.05
+ fnto=1
*
*
*
***************************************************************************
* Extrinsic Source/Drain Diode Model
***************************************************************************
model ndio33 diode
+level   = 1              tnom    = 25
+imax    = 1e10           imelt   = 1e12
+xti     = 3              tlev    = 0              tlevc   = 1
+eg      = 1.17           gap1    = 4.73e-4        gap2    = 636
+is      = js_n33         isw     = jsw_n33        n       = n_n33
+cjo     = cj_n33         vj      = pb_n33         m       = mj_n33
+cjsw    = cjsw_n33       vjsw    = pbsw_n33       mjsw    = mjsw_n33
+rs      = 120p           cta     = cta_n          ctp     = ctp_n
+pta     = pta_n          ptp     = ptp_n
*
***************************************************************************
ends nfet33psp
