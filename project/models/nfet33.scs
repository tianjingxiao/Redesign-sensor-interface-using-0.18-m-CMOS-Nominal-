* Path, Component, Release: cmos7base/rel/Spectre/models/nfet33.scs, tech7_models, mod_cmos7base
* CMVC Revision: 1.3 12/09/26 07:49:04
*
*>  IBM 7RF/7WL  nfet33   2.5V and 3.3V NFET subcircuit
*
***************************************************************************
*
*  SYNTAX:  xxx ( d g s b ) nfet33 l=nnn w=nnn
*
*  REQUIRED:
*            +        w = nnn  <- Design width of RX (meters)
*            +        l = nnn  <- Design length of PC (meters)
*  OPTIONAL:
*            +       as = 0    <- Source diffusion area (meters^2)
*            +       ad = 0    <- Drain diffusion area (meters^2)
*            +       ps = 0    <- Source diffusion perimeter in (meters)
*            +       pd = 0    <- Drain diffusion perimeter in (meters)
*            +      nrd = 0    <- Number of squares of drain diffusion
*            +      nrs = 0    <- Number of squares of source diffusion
*            +    dtemp = 0    <- Device temp rise from ambient (deg C)
*            +       nf = 1    <- number of gate stripes or "fingers" in device
*            +  m (par) = 1    <- number of parallel instances (multiplicity)
*            +     gcon = 1    <- Gate connection switch (1->single-ended,
*                                                         2->double-ended)
*            +      rsx = 50   <- body resistance
*            +       rf = 0    <- RF device flag (0-> nfet33 pcell
*                                                 1-> nfet33_rf pcell)
*            +      gns = 0    <- ESD hierarchical cell layout option
*                                 (0-> silicided gate, 1-> non-silicided gate)
*
*  NOTES:
*
*  1. If nrd or nrs are not passed to the model they will default to 0.
*     This will result in 0 ohms of silicided S/D resistance being placed
*     in series with the device model.
*
*  2. Intrinsic FET model includes PC bounded perimeter only
*
* SUBCIRCUIT SCHEMATIC:
*
*                cwgd             drain
*         ________| |_______________O__________________________________
*        |        | |               |                                  |
*        |                          |                                  |
*        |                          /     Ddb (Includes internal       |
*        |                   rdext  \     FET drain area and STI       |
*        |                          /     bounded perimeter)           |
*        |                      d1  |______|/|__________               |
*        |                          |      |\|          |              |
*        |                          |                   |              |
*        |                          |                   |              |
*        |                      ____|                   |              |
*   gate |             g1   | |      |  b1              |     bulk   ----- cwds
*        O-------\/\/\/-----| |      |-------\/\/\/----o-------O     -----  
*        |         rgate    | |____  |         rsb      |              |
*        |                          |                   |              |
*        |                          |                   |              |
*        |                          |                   |              |
*        |                      s1  |______|/|__________|              |
*        |                          |      |\|                         |
*        |                          \                                  |
*        |                   rsext  /     Dsb (Includes internal       |
*        |                          \     FET source area and STI      |
*        |                          |     bounded perimeter)           |
*        |________| |_______________|__________________________________|
*                 | |               O
*                cwgs             source
*
* IBM CONFIDENTIAL
* (C) 2012 IBM Corporation
*
***************************************************************************
simulator lang=spectre
inline subckt nfet33 (d g s b)
parameters
+  w=0 l=0 nf=1 as=0 ad=0 ps=0 pd=0 nrd=0 nrs=0 
+     gcon=1 rsx=50 dtemp=0 rf=0 par=1 gns=0
+  zad=ad zas=as znrd=nrd znrs=nrs zpd=pd zps=ps
*
* Gate bounded perimeter (identical for source and drain)
+  pgate = (w+ndelrx)
*
* STI S/D bounded perimeter (subtract gate bounded from total passed in)
+  pstis = max(0,(zps-pgate))
+  pstid = max(0,(zpd-pgate))
*
* Mis-match calculations: function of w, l, nf and multiplicity (par)
+  kvt=0.96e-8     kvtw=-0.22u   kvtl=0.2494u
+  kmb=0.9277e-5   kmbw=-1.18u   kmbl=0.2765u
+  sigvth  = kvt/sqrt((w-kvtw)*nf*par*(l-kvtl))
+  sigbeta = kmb/sqrt((w-kmbw)*nf*par*(l-kmbl))
+  mmvth0  = (sigvth * mvth0)         // vth0 mis-match
+  mmbeta  = (sigbeta * mbeta)        // beta mis-match
*
* Flicker noise: statistics function of w, l, nf and multiplicity (par)
+  noi_scale = (noistdn-min(log(sqrt(w*l*nf*par/noidevan)),0))
+  noi_mul   = (exp(dnoin33*noi_scale))
*
* Gate resistance calculation (function of w, l and nf)
+  rge1=((pcrsi*(1-gns)+opnpcrsf*gns)/(nf*gcon))*((0.3u+w/(3*gcon))/(l+lwbpc))
+  rgw1=((1.762e-4/((w-2*wint_n33)*nf)+1.246))
+  rgl1=((2.414e6*(l-2*lint_n33)+0.9962)/2.35)
*
* Substrate resistance calculation for rf=1 case (function of w, l and nf)
+  rsw1=((191.9*log10(1/((w-2*wint_n33)*nf))-599.7))
+  rsl1=((3.374e8*(l-2*lint_n33)-13.11)/129)
*
* Short Channel Switch (2.5V devices)
+  sch=(l<0.4u)
*
* Node voltage warning checks
+  pvlim  = 3.6*(1-sch) + 2.75*sch
+  pblim  = 6.0*(1-sch) + 4.6*sch
*
if (gbv==2) {
 vds_check  assert dev=nfet33 param=vds       min=0 max=pvlim message="Vds exceeds limit" level=warning
 vgs_check  assert dev=nfet33 param=vgs       min=-pvlim max=pvlim message="Vgs exceeds limit" level=warning
 vgd_check  assert dev=nfet33 expr=(vgs-vds)  min=-pvlim max=pvlim message="Vgd exceeds limit" level=warning
 vgb_check  assert dev=nfet33 expr=(vgs-vbs)  min=-pblim max=pblim message="Vgb exceeds limit" level=warning
 vbs_check  assert dev=nfet33 param=vbs       min=-pblim message="Vbs exceeds limit" level=warning
 vbs_chkfwd assert dev=nfet33 param=vbs       max=fwdlim message="Vbs source-body diode is being forward-biased" level=warning
 vbd_check  assert dev=nfet33 expr=(vbs-vds)  min=-pblim message="Vbd exceeds limit" level=warning
 vbd_chkfwd assert dev=nfet33 expr=(vbs-vds)  max=fwdlim message="Vbd drain-body diode is being forward-biased" level=warning
}
*
nfet33 (d1 g1 s1 b1) nch33   w=w l=l as=1e-15 ad=1e-15 ps=pgate pd=pgate 
+                            nrd=0 nrs=0 m=nf trise=dtemp
rdext  d1  d resistor r=(znrd*rsh_n33/nf)
rsext  s1  s resistor r=(znrs*rsh_n33/nf)
rgate  g1  g resistor r=(rge1+rgw1*rgl1)
rsb     b b1 resistor r=((1-rf)*rsx + rf*rsl1*rsw1)
cwgd    g d  capacitor c=(1e-18+rf*(5.7776e-11*w*nf+(0.1744*l*(nf-1)+0.1337*l*nf+0.0749*(nf-1))*1e-11*w*nf))
cwgs    g s  capacitor c=(1e-18+rf*(5.7776e-11*w*nf+(0.1744*l*(nf-1)+0.1337*l*nf+0.0749*(nf-1))*1e-11*w*nf))
cwds    d s  capacitor c=(1e-18+rf*(1.1e-17*w*nf/(l+3.2e-7)+5.5e-18*w*nf/(l+4e-7)+1.6e-11*(l+6e-7)*(nf-1)+2.8e-11*w*(int(nf/2)+int((nf-1)/2))))
ddb     b d1 ndio33 area=zad pj=pstid m=nf trise=dtemp
dsb     b s1 ndio33 area=zas pj=pstis m=nf trise=dtemp
***************************************************************************
* NFET33 BSIM3v3.2.4 Model
***************************************************************************
model nch33  bsim3v3                               type    = n
+version = 3.24           tnom    = 25             tox     = tox_n33
+xj      = 3e-7           nch     = 4e17           vth0    = (vth0_n33+mmvth0)
+k1      = 0.847647       k2      = -0.0377794     k3      = 1.0224
+k3b     = 1.87203        w0      = 7.06769e-8     nlx     = 6.354e-8
+dvt0w   = -3.04192       dvt1w   = 2.82524e6      dvt2w   = -0.0329162
+dvt0    = 3.45458        dvt1    = 0.76917        dvt2    = -0.34
+u0      =(u0_n33+mmbeta) ua      = 1e-20          ub      = 2.22259e-18
+uc      = 9.71848e-11    vsat    = vsat_n33       a0      = 0.841
+ags     = 0.269886       b0      = 1e-8           b1      = 3.26966e-6
+keta    = -0.0436583     a1      = -0.04          a2      = 1
+rdsw    = rdsw_n33       prwg    = 0.0296206      prwb    = -1e-3
+wr      = 1              wint    = wint_n33       lint    = lint_n33
+dwg     = -7.7612e-9     dwb     = -1.77454e-8    voff    = -0.143184
+nfactor = 0.718384       cit     = 0              cdsc    = 0
+cdscd   = 2.94472e-4     cdscb   = 2.88069e-5     eta0    = 2 
+etab    = -0.660586      dsub    = 1.54203        pclm    = 0.693702
+pdiblc1 = 0.0351344      pdiblc2 = 6.19844e-3     pdiblcb = -0.514817
+drout   = 0.29           pscbe1  = 3.66e8         pscbe2  = 4.1074e-5
+pvag    = -0.08          delta   = 5.387e-3       ngate   = 1e20
+alpha0  = 5e-7           alpha1  = 15             beta0   = 24             
+mobmod  = 1              lags    = 0.101219       paramchk= 0
+lua     = 1.20862e-10    lub     = -4.59656e-19   luc     = -2.2546e-11
+lu0     = 0.0129723      lvsat   = -0.125921      ww      = -1.91842e-14
+wk3     = -1             wl      = -1.5e-15       wu0     = -96
+wua     = 2.6468e-11     wub     = -2.7727e-19    wuc     = -2.84e-11
*
***************************************************************************
* Source/Drain Diode Parameters
***************************************************************************
+tlev    = 0              tlevc   = 1
+eg      = 1.17           gap1    = 4.73E-4        gap2    = 636
+cj      = cj_n33         pb      = pb_n33         mj      = mj_n33
+cjsw    = cjsg_n33       pbsw    = pbsg_n33       mjsw    = mjsg_n33
+cjswg   = cjsg_n33       pbswg   = pbsg_n33       mjswg   = mjsg_n33
+xti     = 3              n       = n_n33          rsh     = 0
+js      = js_n33         jsw     = jsw_n33        hdif    = 0
+tcjswg  = tcjsg_n        tpbswg  = tpbsg_n
*
***************************************************************************
* Capacitance-Voltage (CV) Parameters
***************************************************************************
+capmod  = 2              xpart   = 0              dlc     = lint_n33
+dwc     = wint_n33       cgdo    = cgdo_n33       cgso    = cgso_n33
+cgsl    = cgsl_n33       cgdl    = cgdl_n33       ckappa  = 4.0591
+cf      = 0              cgbo    = 4.532e-11      acde    = 1.2
+llc     = 0              lwc     = 0              lwlc    = 0
+wlc     = 0              wwc     = 0              wwlc    = 0
*
***************************************************************************
* Jds/Vt Temperature Parameters
***************************************************************************
+prt     = 461.66         ute     = -1.76          kt1     = -0.276254
+kt1l    = -4.18285e-10   kt2     = -0.0103383     ua1     = 1.8e-9
+ub1     = -3.6e-18       uc1     = -1.35e-10      at      = 2.08e4
+lua1    = -1.4e-10       wat     = 2.41e4         wuc1    = -2e-11
+wute    = 0.176          wua1    = -4e-10         wub1    = 1e-18
*
***************************************************************************
* Flicker Noise Parameters
***************************************************************************
+noimod  = 2                  noia = (3.0e19*noi_mul)  noib = (2.5e5*noi_mul)
+noic    = (-3.0e-12*noi_mul)   em = 5.0e6               af = 1
+ef      = 1.05                 kf = 0
*
***************************************************************************
* Extrinsic Source/Drain Diode Model
***************************************************************************
model ndio33 diode        level   = 1              tnom    = 25
+xti     = 3              tlev    = 0              tlevc   = 1
+eg      = 1.17           gap1    = 4.73e-4        gap2    = 636
+is      = js_n33         isw     = jsw_n33        n       = n_n33
+cjo     = cj_n33         vj      = pb_n33         m       = mj_n33
+cjsw    = cjsw_n33       vjsw    = pbsw_n33       mjsw    = mjsw_n33
+imax    = 1e10           imelt   = 1e12           rs      = 120p
+cta     = cta_n          ctp     = ctp_n          pta     = pta_n
+ptp     = ptp_n
*
***************************************************************************
ends nfet33
