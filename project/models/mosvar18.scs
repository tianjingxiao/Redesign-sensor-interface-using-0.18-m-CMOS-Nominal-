* Path, Component, Release: cmos7base/rel/Spectre/models/mosvar18.scs, tech7_models, mod_cmos7base
* CMVC Revision: 1.2 09/11/04 09:22:03
*
*>  IBM 7RF/7WL  mosvar18   1.8V nMOS Varactor
*
*******************************************************************************
*
*  Syntax:  xxx (g sd sx) mosvar18
*            +     l = 2u    <- length (m)
*            +     w = 2u    <- width  (m)
*            +  xrep = 1     <- number of gates per individual RX diffusion
*            +  yrep = 1     <- number of individual RX diffusion shapes
*            + dtemp = 0     <- temp rise from ambient (deg C)
*            +   rsx = 50    <- Substrate resistance
*
*                            .--------------------.
*                            |                    |
*                            |<-------- l-------->|
*                            |                    |
*                       .----|--------------------|-----.
*                       |    |          ^         |     | RX
*                       |  s |          |         |  d  |
*                       |    |          w         |     |
*                       |    |          |         |     |
*                       |    |          v         |     |
*                        ----|--------------------|-----
*                            |                    |
*                            |          PC        |
*                            |                    |
*                             --------------------
*
* NOTES:
*
*  1. Model assumes source and drain are shorted and dual side PC
*     contacts are shorted.
*
* 
*                    (g)           rtop          
*                     o-----------/\/\/\----(g1)
*                                             |
*                                             /
*                                        rpc  \
*                                             /
*                                             |
*                                      +-----(1)
*                                      |      |
*                                      |      |
*                                      |  ca  = 
*                                      |      |
*                                      |      |
*                                      |    (11)
*                                      |      |
*                                  cf  =      |
*                                      |      |
*                                      |      /
*                                      | rrx  \
*                                      |      /
*                                      |      |
*                                      |      |
*                                      +----(s1)--/\/\/\------------o (sd)
*                                             |    rbot
*                                             |
*                                           - - -
*                                            / \
*                                        dcx - -
*                                             |
*                                             |
*                                        (sx) o (substrate)
*
*
* IBM CONFIDENTIAL
* (C) 2009 IBM Corporation
*
********************************************************************************
simulator lang=spectre

subckt mosvar18 (g sd sx)
parameters    l=2u w=2u xrep=1 yrep=1 dtemp=0 rsx=50 
*
*---- Drawn Geometry ----------------------------------------------
+ pl = max(l,0.18u)
+ pw = max(w,0.4u)
*
*---- Drawn Geometry + Mask Bias ----------------------------------
+ plf  = pl + lwbpc
+ pwf  = pw + delrx
*
*---- Area/Perimeter Calculations ---------------------------------
*  Device values
+ ngdiff   = max(1,xrep)
+ ndiff    = max(1,yrep)
+ par      = ngdiff*ndiff
+ pareac   = plf * pwf * par
*  Nwell values
+ lnw      = (2 * 1.02u) + ((ngdiff-1) * 0.80u) + (ngdiff * pl)
+ wnw      = (2 * 1.08u) + ((ndiff-1) * 1.20u) + (ndiff * pw)
+ areanw   = lnw * wnw
+ perimnw  = 2 * (lnw + wnw)
*---- RX and PC Fringe cap ----------------------------------------
+ prxfrng = 0.44n
+ pcapof  = 0.17n
+ pmetf   = 0.22f
+ pcof    = par * (2 * pwf * pcapof + 2 * plf * prxfrng + pmetf)
*---- Main area Capacitance ---------------------------------------
*    Calculated in Verilog-A component mosvarcap
*---- NW resistance -----------------------------------------------
+ prnw   = (0.9 * nwrs) * (0.5 * plf + 0.80u) / (6 * pwf) + (0.5 * 250p) / (2 * 0.80u * pwf)
*---- Gate resistance ---------------------------------------------
+ prgate = pcrsi * pwf / (12 * plf) + (exp(oppcrenf)-97) * 0.62p / (pwf * plf)
*
*---- Metal line resistances ----------------------
*     value below is M1 sheet resistance and thickness
*     Gates (with W = 0.60 um)
+ prtop = (m1rsi / .60u) * (plf + .80u) * ngdiff / 3
*     Source/Drains 
+ prbot = m1rs * (plf + 0.80u) * ngdiff / (3*(pwf/2-0.02u))
*

*  Subcircuit
rtop  g  g1  resistor r=prtop / (ndiff+1) trise=dtemp  tc1=tm1rs
rpc  g1   1  resistor r=prgate / par      trise=dtemp  tc1=tpcrsi
ca    1  11  mosvarcap 
+   pl_va = plf
+   pw_va = pwf
+  pnf_va = par
+   co_va = co_mv + 0.067n/plf
+   qs_va = qs_mv + 9.5p/plf
+   qp_va = qp_mv - 0.25n/plf
+  pon_va = 8m
+  vbi_va = 0.035 + 0.003u/plf
+  cot_va = 0.1m
+ pont_va = 5m
+ vbit_va = 300u
+ deltemp = temp+dtemp-25
rrx  11  s1  resistor r=prnw / par    trise=dtemp  tc1=tnwrs
rbot s1  sd  resistor r=prbot / ndiff trise=dtemp  tc1=tm1rs
* 
cfr   1  s1  capacitor c=pcof
* 
dsx  sx  s1  nwsxd  area=areanw perim=perimnw trise=dtemp
*
*---- NWell-to-Sub Diode ---------------------------------------
model nwsxd diode
+ level = 1
+  tlev = 2
+ tlevc = 1
+  tnom = 25
+    eg = 1.12
+   xti = 3.0
+  gap1 = 473u
+  gap2 = 636
+     n = 1.023
+    ns = 1.023
+    is = 1.037u
+   isw = 2.873p
+    rs = rsx*areanw
+   cjo = 0.18m
+    vj = 4.0
+     m = 1.2
+  cjsw = 0.4n
+  vjsw = 0.1
+  mjsw = 1.2
+  imax = 1e15 
+    bv = 15
* 
ends mosvar18
