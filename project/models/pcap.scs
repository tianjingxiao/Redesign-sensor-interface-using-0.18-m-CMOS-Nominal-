* Path, Component, Release: cmos7base/rel/Spectre/models/pcap.scs, tech7_models, mod_cmos7base
* CMVC Revision: 1.1 09/08/28 10:14:53
*
*>  IBM 7RF/7WL	 pcap	pMOS Varactor (Thin oxide)
*
*******************************************************************************
*
*  Syntax:  xxx (g sd tw) pcap
*	     +	   l = 2u    <- length (m)
*	     +	   w = 2u    <- width  (m)
*	     +	xrep = 1     <- number of gates per individual RX diffusion
*	     +	yrep = 1     <- number of individual RX diffusion shapes
*	     + dtemp = 0     <- temp rise from ambient (deg C)
*	     +	 rsx = 50    <- Substrate resistance
*
*			     .--------------------.
*			     |			  |
*			     |<-------- l-------->|
*			     |			  |
*			.----|--------------------|-----.
*			|    |		^	  |	| RX
*			|  s |		|	  |  d	|
*			|    |		w	  |	|
*			|    |		|	  |	|
*			|    |		v	  |	|
*			 ----|--------------------|-----
*			     |			  |
*			     |		PC	  |
*			     |			  |
*			      --------------------
*
* NOTES:
*
*  1. Model assumes source and drain are shorted and dual side PC
*     contacts are shorted.
*
*
*		     (g)	   rtop
*		      o-----------/\/\/\----(g1)
*					      |
*					      /
*					 rpc  \
*					      /
*					      |
*				       +-----(1)
*				       |      |
*				       |      |
*				       |  ca  =
*				       |      |
*				       |      |
*				       |    (11)
*				       |      |
*				   cf  =      |
*				       |      |
*				       |      /
*				       | rrx  \
*				       |      /
*				       |      |
*				       |      |
*				       +----(s1)--/\/\/\------------o (sd)
*					      |	   rbot
*					      |
*					    - - -
*					     / \
*					 dcx - -
*					      |
*					      |
*					 (sx) o (substrate)
*
*
* IBM CONFIDENTIAL
* (C) 2009 IBM Corporation
*
********************************************************************************
*
simulator lang=spectre
*
subckt pcap (g sd tw)
parameters  l=2u w=2u xrep=1 yrep=1 dtemp=0 rsx=50
*
*---- Drawn Geometry ----------------------------------------------
+ pl = max(l,0.18u)
+ pw = max(w,0.4u)
*
*---- Drawn Geometry + Mask Bias ----------------------------------
+ plf = pl + lwbpc
+ pwf = pw + delrx
*
*---- Area/Perimeter Calculations ---------------------------------
*  Device values
+ ngdiff  = max(1,xrep)
+ ndiff	  = max(1,yrep)
+ par	  = ngdiff*ndiff
+ pareac  = (plf-0.0u) * pwf * par
+ pedgec  = 0.0u * pwf * par
*  Nwell values
+ lpw	  = (2 * 0.74u) + ((ngdiff-1) * 0.80u) + (ngdiff * pl)
+ wpw	  = (2 * 0.90u) + ((ndiff-1) * 1.20u) + (ndiff * pw)
+ areapw  = lpw * wpw
+ perimpw = 2 * (lpw + wpw)
*---- RX and PC Fringe cap ----------------------------------------
+ prxfrng = 0.43n
+ pcapof  = 0.14n
+ pmetf	  = 0.24f
+ pcof	  =  par * (2 * pwf * pcapof + 2 * plf * prxfrng + pmetf)
*---- PW resistance -----------------------------------------------
+ prvpw	 = 1.0n
+ prpw	 = pwirs * (plf + 2*0.8u) / (12 * pwf) + 0.7 * prvpw / (2 * 0.8u * pwf)
*---- Gate resistance ---------------------------------------------
+ prgate = pcrsi * pwf / (12 * plf) + 1p / (pwf * plf)
*
*---- Metal line resistances ----------------------
*     value below is M1 sheet resistance and thickness
*     Gates (with W = 0.60 um)
+ prtop = (m1rsi / .60u) * (plf + .80u) * ngdiff / 3
*     Source/Drain
+ prbot = m1rs * (plf + 0.80u) * ngdiff / (3*(pwf/2-0.02u))
*
*  Subcircuit
rtop  g	 g1  resistor r= prtop/(ndiff+1)  trise=dtemp tc1=tm1rs
rpc  g1	  1  resistor r= prgate / par	  trise=dtemp tc1=tpcrsi
xca   1	 11  pvarcap
+ pareac = pareac
+ co_pva = co_pmv
+ cot_pva = 0.15m
+ qp_pva = qp_pmv
+ qs_pva = qs_pmv + 77.5p/plf
+ pon_pva = 6.92m - 2.68n/plf - 21.25n/pwf
+ pont_pva = 3m
+ vbi_pva = -0.074 +0.007u/plf
+ vbit_pva = -0.55m
+ deltemp =  temp+dtemp-25
rrx  11	 s1  resistor r= prpw / par   trise=dtemp tc1=tnwrs
rbot s1	 sd  resistor r= prbot/ndiff  trise=dtemp tc1=tm1rs
*
cfr   1	 s1  capacitor c=pcof
*
dpwpi s1 tw  pidio  area=areapw	 pj=perimpw  trise=dtemp
*
*---- PWell-to-PI Diode ---------------------------------------
model pidio diode
+ level = 1
+  tlev = 2
+ tlevc = 1
+  tnom = 25
+    eg = 1.11
+   xti = 3.0
+  gap1 = 472u
+  gap2 = 636
+    rs = rsx*areapw
+     n = n_pwi
+    ns = n_pwi
+    is = js_pwi
+   isw = jsw_pwi
+    cj = cj_pwi
+   cta = cta_pwi
+    vj = pb_pwi
+   pta = pta_pwi
+     m = mj_pwi
+  cjsw = cjsw_pwi
+   ctp = ctp_pwi
+  vjsw = pbsw_pwi
+   ptp = ptp_pwi
+  mjsw = mjsw_pwi
+    bv = 10
+ jmelt = 1e15
*
ends pcap
