* Path, Component, Release: cmos7base/rel/Spectre/models/dipdnw.scs, tech7_models, mod_cmos7base
* CMVC Revision: 1.1 09/07/29 15:46:06
*
*>  IBM 7RF/7WL  dipdnw    P+ Nwell Diode - voltage reference diode
*
***********************************************************************
*                               
*                         anode  o
*                                |
*                                |
*                               ----    
*                      P+ Nwell \  /      
*                         Diode  \/          
*                               ----    
*                                |      
*                                |      
*                                |            
*                            SX  o   
*                                    
***********************************************************************
*
*  DIMENSIONS: W = Design width  of RX
*              L = Design length of RX
*
*  SYNTAX:     Specify both width and length and number of fingers:
*              xxx  a  sx dipdnw (w=2u  l=5u  nf=1)
*
*  OPTIONAL:   dtemp --> Device temperature difference with respect to
*                        the simulated circuit temperature (deg C).
*
*	 	par  --> Explicit device multiplicity.
*
*
*  NOTES:
*
*  1. Model supports only rectangular diode with cathode connected to
*     ground (SX).
*
* IBM CONFIDENTIAL
* (C) 2009 IBM Corporation
*                                
***********************************************************************
simulator lang=spectre

subckt dipdnw  (a  sx)
parameters     w=1.6u  l=10u  nf=1  par=1  dtemp=0  
*
* Check for minimum/maximum width, calculate effective width
+ aw = max(w,0.8u)
+ anodew = min(aw,2u) + delrx
*
* Check for minimum length, calculate effective length
+ anodel = max(l,2u) + delrx
*
* Calculate area & perimeter for capacitance
+ totp  = 2 * nf * (anodel + anodew)
+ tota = nf * anodel * anodew
*
* Calculate the resistance, a function of length of cathode RX.
*   dirs is the vertical resistance through the diode 
+ dirs = 50p/tota
*rnw is the resistance through the nw
+ rnw = (nwrs*anodew)/(12*anodel*nf)+(nwrs*0.34u)/(2*anodel*nf)+(nwrs*0.6u)/(6*anodel*nf)
*rvnw is the vertical resistance through the nw (nw contact)
+ rvnw = nrnw/(anodel*0.6u*(nf+1))
+ rstot = dirs + (rnw + rvnw)/2
*
* Mis-match Calculation
+ dima = 3.0
+ diml = 0.0
+ dimw = 0.5
+ dioma   = dima * dima / (anodew * anodel * nf * 1e12)
+ dioml   = diml * diml / (anodel * anodel * nf * nf * 1e12)
+ diomw   = dimw * dimw / (anodew * anodew * 1e12)
+ dio3sig = 0.01 * sqrt(dioma + dioml + diomw)
+ diomm   = dio3sig * 0.7071 / sqrt(par)
+ mmatd  = 1 + (diomm * mmdio)

*  Diode network
dpri  a  sx   didio  area=tota  perim=totp  trise=dtemp

* P+ to NW Diode
* rs is multiplied by area as rstot is acutal resistance and rs 
*   gets divided by area of device
*
model didio diode
+ level = 1
+  tlev = 2
+ tlevc = 1
+  tnom = 25
+    eg = 1.12
+   xti = 3.0
+  gap1 = 473u
+  gap2 = 636
+    rs = rstot*tota
+     n = 1.006
+    ns = 1.006
+    is = js_p*(1 + tdeldi)*mmatd
+   isw = jsw_p*(1 + tdeldi)*mmatd
+    cj = cj_p
+   cta = cta_p
+    vj = pb_p
+   pta = pta_p
+     m = mj_p
+  cjsw = cjsw_p
+   ctp = ctp_p
+  vjsw = pbsw_p
+   ptp = ptp_p
+  mjsw = mjsw_p
+  jmax = 5e5/(1 + (jsw_p*totp)/(js_p*tota))
+ jmelt = 1e13
+    bv = 10.5

ends dipdnw
